# What were the starboard fan speed values on 01.03.2025 when the engine room temperature exceeded 24Â°C?

PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX ex:   <http://example.org/individuals#>
PREFIX dbc:  <http://example.org/dbc#>

SELECT ?templog ?tempvalue ?timestamp ?rpmlog ?rpmvalue
WHERE {
  ?templog a dbc:SignalLog ;
           sosa:observedProperty ex:EngineRoomTemperature ;
           sosa:hasSimpleResult ?tempvalue ;
           sosa:resultTime ?timestamp .
  FILTER(?tempvalue > 24.0)
  FILTER(STRSTARTS(STR(?timestamp), "2025-03-01"))

  OPTIONAL {
    ?rpmlog a dbc:SignalLog ;
            sosa:observedProperty ex:StarboardFanRotationSpeed ;
            sosa:hasSimpleResult ?rpmvalue ;
            sosa:resultTime ?rpmtimestamp .
	#Joining on timestamps with cut-off microseconds 
    FILTER(SUBSTR(STR(?timestamp), 1, 19) = SUBSTR(STR(?rpmtimestamp), 1, 19))
  }
}
ORDER BY ?timestamp




